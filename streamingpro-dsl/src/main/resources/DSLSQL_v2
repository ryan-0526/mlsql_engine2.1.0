
grammar DSLSQL;

@header {
package streaming.dsl.parser;
}

//load jdbc.`mysql1.tb_v_user` as mysql_tb_user;
//save csv_input_result as json.`/tmp/todd/result_json` partitionBy uid;
statement
    : (sql ender)*
    ;


sql
    : LOAD format '.' path whereExpressions? booleanExpression* AS tableName
    | SAVE saveMode* tableName AS format '.' path whereExpressions? (PARTITIONBY col? colGroup*)?
    | SELECT ~(';')* AS tableName
    | INSERT ~(';')*
    | CREATE ~(';')*
    | DROP ~(';')*
    | REFRESH ~(';')*
    | SET setKey '=' setValue where? expression? booleanExpression*
    | CONNECT format where? expression? booleanExpression* (AS db)?
    | (TRAIN|RUN|PREDICT) tableName AS format '.' path whereExpressions? asTableName*
    | REGISTER format '.' path AS functionName whereExpressions?
    | UNREGISTER format '.' path whereExpressions?
    | INCLUDE format '.' path whereExpressions?
    | EXECUTE_COMMAND (setValue|setKey|commandValue)*
    | SIMPLE_COMMENT
    ;

//keywords
AS: 'as';
LOAD: 'load';
SAVE: 'save';
SELECT: 'select';
INSERT: 'insert';
CREATE: 'create';
DROP: 'drop';
REFRESH: 'refresh';
SET: 'set';
CONNECT: 'connect';
TRAIN: 'train';
RUN: 'run';
PREDICT: 'predict';
REGISTER: 'register';
UNREGISTER: 'unregister';
INCLUDE: 'include';
OPTIONS:'options';
WHERE:'where';
PARTITIONBY:'partitionBy'|'partitionby';
OVERWRITE:'overwrite';
APPEND:'append';
ERRORIfExists:'errorIfExists';
IGNORE:'ignore';


saveMode: (OVERWRITE|APPEND|ERRORIfExists|IGNORE);

where: OPTIONS|WHERE;

whereExpressions: where expression? booleanExpression*;


booleanExpression
    : 'and' expression
    ;

expression
    : qualifiedName '=' (STRING|BLOCK_STRING)
    ;

ender
    :';'
    ;

format
    : identifier
    ;

path
    : quotedIdentifier | identifier
    ;

setValue
    : qualifiedName | quotedIdentifier | STRING | BLOCK_STRING
    ;

setKey
    : qualifiedName
    ;

command
    : EXECUTE_COMMAND
    ;


commandValue
    : (LETTER | DIGIT | '_' | '-' | '/' | '>' | '<' | '.' | '~')+
    ;


db
    :qualifiedName | identifier
    ;

asTableName
    : AS tableName
    ;

tableName
    : identifier
    ;

functionName
    : identifier
    ;

colGroup
    : ',' col
    ;

col
    : identifier
    ;

qualifiedName
    : identifier ('.' identifier)*
    ;

identifier
    : strictIdentifier
    ;

strictIdentifier
    : IDENTIFIER
    | quotedIdentifier
    ;

quotedIdentifier
    : BACKQUOTED_IDENTIFIER
    ;



EXECUTE_COMMAND
    : EXECUTE_TOKEN (LETTER | DIGIT | '_')+
    ;


EXECUTE_TOKEN: '!';

STRING
    : '\'' ( ~('\''|'\\') | ('\\' .) )* '\''
    | '"' ( ~('"'|'\\') | ('\\' .) )* '"'
    ;

BLOCK_STRING
    : '\'\'\'' ~[+] .*? '\'\'\''
    ;

IDENTIFIER
    : (LETTER | DIGIT | '_')+
    ;

BACKQUOTED_IDENTIFIER
    : '`' ( ~'`' | '``' )* '`'
    ;

fragment DIGIT
    : [0-9]
    ;

fragment LETTER
    : [a-zA-Z]
    ;

SIMPLE_COMMENT
    : '--' ~[\r\n]* '\r'? '\n'? -> channel(HIDDEN)
    ;

BRACKETED_EMPTY_COMMENT
    : '/**/' -> channel(HIDDEN)
    ;

BRACKETED_COMMENT
    : '/*' ~[+] .*? '*/' -> channel(HIDDEN)
    ;

WS
    : [ \r\n\t]+ -> channel(HIDDEN)
    ;

// Catch-all for anything we can't recognize.
// We use this to be able to ignore and recover all the text
// when splitting statements with DelimiterLexer
UNRECOGNIZED
    : .
    ;
